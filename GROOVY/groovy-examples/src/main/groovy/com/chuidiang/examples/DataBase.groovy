package com.chuidiang.examples

import groovy.sql.Sql

import java.sql.ResultSet

/**
 * Created by JAVIER on 25/03/2017.
 */
class DataBase {
    static void main(String [] args) {
        def url = 'jdbc:hsqldb:mem:yourDB'
        def user = 'sa'
        def password = ''
        def driver = 'org.hsqldb.jdbcDriver'
        def sql = Sql.newInstance(url, user, password, driver)

        sql.execute('''CREATE TABLE Author (
                id          INTEGER GENERATED BY DEFAULT AS IDENTITY,
                firstname   VARCHAR(64),
                lastname    VARCHAR(64)
        );''')

        for (name in ['Pedro':'Gomez','Juan':'Lopez','Antonio':'Garcia']) {
            def ids  = sql.executeInsert("""
                insert into Author
                values (null,?,?)
                """,
                [name.key, name.value])

            ids.each {println it}

            ids  = sql.executeInsert("""
                insert into Author
                values (null, $name.key, $name.value)
                """)

            ids.each {println it}

            def person = [name:name.key, surname:name.value]

            ids  = sql.executeInsert("""
                insert into Author
                values (null, :name, :surname)
                """, person)

            ids.each {println it}

            person = new Person(name:name.key, surname:name.value)

            ids  = sql.executeInsert("""
                insert into Author
                values (null, :name, :surname)
                """, person)

            ids.each {println it}

        }

        sql.eachRow("select * from Author") { row ->
            println "${row.id} ${row.firstname} ${row.lastname}"
        }

        sql.close()
    }
}

class Person {
    String name
    String surname
}
